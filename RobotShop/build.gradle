// this block is necessary to make enforcedPlatform work for Quarkus plugin available
// only locally (snapshot) that is also importing the Quarkus BOM
buildscript {
    repositories {
        mavenLocal()
    }
    dependencies {
        classpath "io.quarkus:quarkus-gradle-plugin:${quarkusVersion}"
    }
}

plugins {
    id 'java'
}

apply plugin: 'io.quarkus'

repositories {
    mavenLocal()
    mavenCentral()
    flatDir {
        def libDir = new File(project(':RobotCommons').buildDir, project(':RobotCommons').libsDirName)
        dirs libDir
    }
}

dependencies {
    // Quarkus can only find our dependencies in jar form. Compiling RobotCommons normally results in the class directory on the classpath, not the jar
    implementation 'com.example.big.troublesome.corp:RobotCommons:0.0.1@jar'
    implementation enforcedPlatform("io.quarkus:quarkus-bom:${quarkusVersion}")
    implementation 'io.quarkus:quarkus-smallrye-rest-client'
}

processResources {
    filesMatching("**/application.properties") {
        expand(project.properties)
    }
}

tasks.compileJava.dependsOn(':RobotCommons:jar')
